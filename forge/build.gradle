plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    forge {
        mixinConfig "galacticconquest.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    implementation("thedarkcolour:kotlinforforge:$forge_kotlin_version")

    // Mixin Extras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${mixin_extras}"))
    // Mixin Squared
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixin_squared}"))
    implementation(include("com.github.bawnorton.mixinsquared:mixinsquared-forge:${mixin_squared}"))
    // Conditional Mixin
    implementation(include("me.fallenbreath:conditional-mixin-forge:${conditional_mixin}"))

    // VS2
    modApi("org.valkyrienskies:valkyrienskies-120-forge:${vs2_version}") { transitive = false }
    forgeRuntimeLibrary implementation("org.valkyrienskies.core:api:${vs_core_version}") { transitive = false }
    //implementation("org.valkyrienskies.core:api-game:${vs_core_version}") { transitive = false }
    forgeRuntimeLibrary implementation("org.valkyrienskies.core:util:${vs_core_version}") { transitive = false }
    //forgeRuntimeLibrary implementation("org.valkyrienskies.core:impl:${vs_core_version}") { transitive = false }
    forgeRuntimeLibrary implementation("thedarkcolour:kotlinforforge:${forge_kotlin_version}")

    // region Manually include every single dependency of vs-core (total meme)
    /*forgeRuntimeLibrary include("org.valkyrienskies.core:api:${vs_core_version}") {
        transitive = false
    }

    forgeRuntimeLibrary include("org.valkyrienskies.core:util:${vs_core_version}") {
        transitive = false
    }*/

    forgeRuntimeLibrary implementation("javax.inject:javax.inject:1") { transitive = false }

    // JOML for Math
    forgeRuntimeLibrary implementation("org.joml:joml:1.10.4") { transitive = false }
    forgeRuntimeLibrary implementation("org.joml:joml-primitives:1.10.0") { transitive = false }

    // Apache Commons Math for Linear Programming
    forgeRuntimeLibrary implementation("org.apache.commons:commons-math3:3.6.1") { transitive = false }

    // Jackson Binary Dataformat for Object Serialization
    def jacksonVersion = '2.14.0'
    // forked to remove module-info
    forgeRuntimeLibrary implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion-rubyfork") { transitive = false }
    forgeRuntimeLibrary implementation("com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion") { transitive = false }
    forgeRuntimeLibrary implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jacksonVersion") { transitive = false }
    forgeRuntimeLibrary implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion") { transitive = false }
    forgeRuntimeLibrary implementation("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion") { transitive = false }
    forgeRuntimeLibrary implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion") { transitive = false }
    forgeRuntimeLibrary implementation("com.github.Rubydesic:jackson-kotlin-dsl:1.2.0") { transitive = false }
    forgeRuntimeLibrary implementation("com.networknt:json-schema-validator:1.0.71") { transitive = false }
    forgeRuntimeLibrary implementation("com.ethlo.time:itu:1.7.0") { transitive = false }
    forgeRuntimeLibrary implementation("com.github.victools:jsonschema-module-jackson:4.25.0") { transitive = false }
    forgeRuntimeLibrary implementation("com.github.victools:jsonschema-generator:4.25.0") { transitive = false }
    forgeRuntimeLibrary implementation("com.fasterxml:classmate:1.5.1") { transitive = false }
    forgeRuntimeLibrary implementation("com.flipkart.zjsonpatch:zjsonpatch:0.4.11") { transitive = false }
    forgeRuntimeLibrary implementation("org.apache.commons:commons-collections4:4.3") { transitive = false }
    forgeRuntimeLibrary implementation("com.google.dagger:dagger:2.43.2") { transitive = false }
    // endregion

    modImplementation("com.lowdragmc.photon:photon-forge-${rootProject.minecraft_version}:${rootProject.photon_version}") { transitive = false }
    modImplementation("com.lowdragmc.ldlib:ldlib-forge-${rootProject.minecraft_version}:${rootProject.ldlib_version}") { transitive = false }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
