plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"

    modImplementation "dev.architectury:architectury-fabric:$architectury_api_version"

    // Cloth Config
    include(modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"))

    modApi("com.terraformersmc:modmenu:${modmenu_version}")

    modImplementation("net.fabricmc:fabric-language-kotlin:${fabric_kotlin_version}+kotlin.${kotlin_version}")

    // Mixin Extras
    implementation(include(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}")))
    // Mixin Squared
    implementation(include("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${mixin_squared_version}"))
    // Conditional Mixin
    implementation(include("me.fallenbreath:conditional-mixin-fabric:${conditional_mixin_version}"))

    // VS2
    modApi("org.valkyrienskies:valkyrienskies-120-fabric:${vs2_version}") { transitive = false }
    compileOnly("org.valkyrienskies.core:api:${vs_core_version}")
    compileOnly("org.valkyrienskies.core:api-game:${vs_core_version}")
    compileOnly("org.valkyrienskies.core:util:${vs_core_version}")
    compileOnly("org.valkyrienskies.core:impl:${vs_core_version}")
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.10"
    api "org.jetbrains.kotlin:kotlin-reflect:1.9.10"

    // LDLib, Photon, Shimmer
    modImplementation("com.lowdragmc.shimmer:Shimmer-fabric:${minecraft_version}-${shimmer_version}") { transitive = false }
    modImplementation("com.lowdragmc.photon:photon-fabric-${minecraft_version}:${photon_version}") { transitive = false }
    modImplementation("com.lowdragmc.ldlib:ldlib-fabric-${minecraft_version}:${ldlib_version}") { transitive = false }

    // Gravity API
    modImplementation("com.github.qouteall:GravityChanger:v${fabric_gravity_api_version}-mc${minecraft_version}")

    // CICADA

    // Do a Barrel Roll
    modRuntimeOnly("dev.isxander.yacl:yet-another-config-lib-fabric:${yacl_version}") {
        exclude group: "net.fabricmc"
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
